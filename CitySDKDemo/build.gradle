apply plugin: 'android-sdk-manager'
apply plugin: 'android'

android {
    compileSdkVersion 19
    buildToolsVersion "19.1.0"

    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
    }

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 19
    }

    buildTypes {
        debug {
            buildConfigField "String", "AUTHORITY", "\"${DEBUG_PROVIDER}\""
        }

        release {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    signingConfigs {
        debug {
            storeFile file('../debug.keystore')
        }
        release {
            storeFile file('../release.keystore')
        }
    }

    applicationVariants.all { variant ->
        // This is an annoying hack to get around the fact that the Gradle plugin does not support
        // having libraries with different minSdkVersions. Play Services has a min version of 9 (Gingerbread)
        // but Android Maps Utils supports 8 (Froyo) still
        variant.processManifest.doFirst {
            File manifestFile = file(
                    "${buildDir}/exploded-aar/com.google.maps.android/android-maps-utils/0.3/AndroidManifest.xml")
            if (manifestFile.exists()) {
                println("Replacing minSdkVersion in Android Maps Utils")
                String content = manifestFile.getText('UTF-8')
                content = content.replaceAll(/minSdkVersion="8"/, 'minSdkVersion=\"9\"')
                manifestFile.write(content, 'UTF-8')
                println(content)
            }
        }
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:+'
    compile 'com.google.android.gms:play-services:4+'
    compile 'com.google.maps.android:android-maps-utils:0.3+'
    compile 'org.apache.commons:commons-lang3:3.1'
    compile 'com.googlecode.ez-vcard:ez-vcard:0.9.2'
    compile 'org.mnode.ical4j:ical4j:1.0.2'


    compile group: 'com.nostra13.universalimageloader', name: 'universal-image-loader',
            version: '1.9.1'

    compile group: 'org.codeforamerica.open311', name: 'jreport',
            version: '0.0.4'

    compile fileTree(dir: 'libs', include: ['*.jar'])
}
